implementation unmanaged;

// behavior defintion for the TRAVEL root entity
define behavior for /DMO/I_Travel_U24 alias travel implementation in class /DMO/BP_TRAVEL_U24 unique
etag LastChangedAt

{
 field (read only) TravelID;
 field (mandatory) AgencyID, CustomerID, BeginDate, EndDate;

 create;
 update;
 delete;

 action set_status_booked result [1] $self;

 association _Booking { create; }

}

// behavior defintion for the BOOKING child entity
define behavior for /DMO/I_Booking_U24 alias booking implementation in class /DMO/BP_BOOKING_U24 unique

{
  field (read only) TravelID, BookingID;
  field (mandatory) BookingDate, CustomerID, AirlineID, ConnectionID, FlightDate;

  create;
  update;
  delete;

  association _BookSupplement abbreviation _Supplement { create; }

}

// behavior defintion for the BOOKING SUPPLEMENT sub node
define behavior for /DMO/I_BOOKINGSUPPLEMENT_U24 alias bookingsupplement implementation in class /DMO/BP_BOOKINGSUPPLEMENT_U24 unique
{

  field (read only) TravelID, BookingID, BookingSupplementID ;
  field (mandatory) SupplementID, Price;

  create;
  update;
  delete;
}
